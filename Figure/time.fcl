FUNCTION_BLOCK time

// Define input variables
VAR_INPUT				
	play_vs_time : REAL; //how many seconds has been played when a play is made by a player
	play_vs_margin : REAL; //what is the margin beetween the two teams when the play is made
	minutes_played : REAL; //minutes played by a player in the game
END_VAR

// Define output variable
VAR_OUTPUT				
	time_index : REAL;
END_VAR

// Fuzzify input variable 'play_vs_time': {'scarso', 'accettabile' , 'sufficiente', 'buono', 'eccellente'}
FUZZIFY play_vs_time	
	TERM scarso_worst := (1200, 1) (1800, 0);	
	TERM accettabile := (1200, 0) (1800, 1) (2100, 1) (2280, 0);
	TERM sufficiente := (2100, 0) (2280, 1) (2340, 1) (2380, 0);
	TERM buono := (2340, 0) (2380, 1) (2390, 1) (2399, 0);
	TERM eccellente_best := (2390, 0) (2399, 1); 	
END_FUZZIFY

// Fuzzify input variable 'play_vs_margin': { 'scarso', 'accettabile' , 'sufficiente', 'buono', 'eccellente' }
FUZZIFY play_vs_margin			
	TERM eccellente_best := (1, 1) (3, 0);	
	TERM buono := (1, 0) (3, 1) (5, 1) (10, 0);
	TERM sufficiente := (5, 0) (10, 1) (15, 1) (20, 0);
	TERM accettabile := (15, 0) (20, 1) (25, 1) (30, 0);
	TERM scarso_worst := (25, 0) (30, 1); 
END_FUZZIFY

// Fuzzify input variable 'minutes_played': { 'scarso', 'accettabile' , 'sufficiente', 'buono', 'eccellente' }
FUZZIFY minutes_played			
	TERM scarso_worst := (10, 1) (18, 0) ; 
	TERM accettabile := (10, 0) (18, 1) (22, 1) (28, 0);
	TERM sufficiente := (22, 0) (28, 1) (32, 1) (35, 0);
	TERM buono := (32, 0) (35, 1) (37, 1) (38, 0);
	TERM eccellente_best := (37, 0) (38, 1);
END_FUZZIFY

// Defuzzify output variable 'time_index' : {'scarso', 'accettabile' , 'sufficiente', 'buono', 'eccellente' }
DEFUZZIFY time_index			
	TERM scarso_worst := (0, 1) (0.25, 0) ; 
	TERM accettabile := (0, 0) (0.25, 1) (0.5, 0);
	TERM sufficiente := (0.25, 0) (0.5, 1) (0.75, 0);
	TERM buono := (0.5, 0) (0.75, 1) (1, 0);
	TERM eccellente_best := (0.75, 0) (1, 1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Inference rules
RULEBLOCK FIND
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

	// the two rules required by a F-IND index, with worst and best cases
	RULE 1 : IF play_vs_time IS scarso_worst AND play_vs_margin IS scarso_worst AND minutes_played IS scarso_worst THEN time_index IS scarso_worst;
	RULE 2 : IF play_vs_time IS eccellente_best AND play_vs_margin IS eccellente_best AND minutes_played IS eccellente_best THEN time_index IS eccellente_best;
END_RULEBLOCK



END_FUNCTION_BLOCK
